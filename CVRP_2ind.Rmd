---
title: "2 Index Capacitated Vehicle Routing Problem"
author: "Yantralogics LLC"
date: "2023-06-23"
output: html_document
---

```{r setup, include=FALSE}
knitr::opts_chunk$set(echo = TRUE)
```

## 2 index Vehicle Routing Problem Formulation

In this tutorial we will take a look at a couple of formulations for vehicle routing problem. We will first discuss a two index vehicle flow formulation that uses $O(n^2)$ binary variables $x$ to indicate if a vehicle traverses on an arc in the optimal solution. In other words, variable $x_{ij}$ takes the value 1 if arc $(i,j) \in A$ belongs to the optimal solution and takes value 0 otherwise.

We designate cost of travelling on arc $(i,j)$ with $c_{ij}$. In this formulation, the vehicles share a common capacity value $C$. The demand/load at each city is designated by $d_i$. Unless otherwise stated, we will consider node $\{0\}$ to be the origin and final destination. The number of trucks are designated by $K$.

It stands to reason that simplest way to define the cost function is to sum the distance travelled by all the vehicles and all the arcs covered.

$$min \sum_{i \in V} \sum_{j \in V} c_{ij}x_{ij}$$
subject to

$$\sum_{i \in V} x_{ij} = 1 ,\forall j \in V \setminus \{0\}, $$
This constraint assures that except for the origin, each city must be entered exactly once. 

$$\sum_{j \in V} x_{ij} = 1 ,\forall i \in V \setminus \{0\}, $$
This constraint assures that except for the origin , each city will be exited exactly once.

$$\sum_{i \in V} x_{i0} = K,$$
This constraint states that at the origin, exactly $K$ vehicles should return. 

$$\sum_{j \in V} x_{0j} = K,$$
This constraint is required for assuring that from the origin, exactly $K$ vehicles depart.

In addition to these standard constraints, we need to impose subtour elimination constraints. There are multiple ways to write these constraints, but the MTZ formulation for subtour elimination is most intuitive and straight forward to implement in integer programming realm. The central concept is as follows. A new integer variable is introduced $u_i,i \in V \setminus \{0\}$. This represents the load of the vehicle after visiting customer $i$. 

$$u_i - u_j +Cx_{ij} \le C - d_j , \forall i, j \in V \setminus \{0\}$$

$$d_i \le u_i \le C , \forall i \in V \setminus \{0\}$$

Above two constraints impose both the capacity and connectivity requirements. When $x_{ij}=0$, constraint is not binding since $u_i \le C$ and $u_j \ge d_j$. Where as when $x_{ij}=1$, they impose that $u_j \ge u_i + d_j$. 

## AMPL Implementation

Now that we have the equations written down, how shall we implement this in the AMPL? Consider following code chunk.

```ampl
# Two index formulation : 
# n = number of cities
param n>=0;
# k = number of trucks
param K>=0;
# C = capacity of each truck - same capacity
param C >=0;

# Create the set for all the nodes - 0 indicating the depot
set N :=0..n;
# Create the set of arcs 
set A :={i in N,j in N:i<>j};

param c{A}>=0;
# d = demand vector at all the cities except for the depot 
param d{1..n};

# decision variable
var x{A} binary;
# additional decision variable to remove subtours per MTZ formulation
var u{1..n} >=0;

# Objective function is sum of all distances travelled
minimize cost: sum{i in N,j in N:i<>j} c[i,j]*x[i,j];

# Each city must be entered only once
s.t. C1{j in 1..n}:sum{i in N:i<>j} x[i,j]=1;
# Each city must be exited only once
s.t. C2{i in 1..n}:sum{j in N:i<>j} x[i,j]=1;
# From depot, exactly K trucks should depart
s.t. C3:sum{j in 1..n} x[0,j]=K;
# At the depot, exactly K trucks should arrive
s.t. C31:sum{i in 1..n} x[i,0]=K;

# MTZ dictated subtour elimination constraint 1
s.t. C4{i in 1..n,j in 1..n:i<>j}: u[i] - u[j] + C*x[i,j] <= C- d[j];
# MTZ dictated subtour elimination constraint 2 
s.t. C5{i in 1..n}:d[i]<= u[i] <= C;



```


```ampl

```


## 2 index Open Vehicle Routing Problem Formulation


```{r, echo=F,message=F,warning=F}
library(tidyverse)
library(knitr)
knitr::include_graphics(here::here('www/‎VRP_schematics_open.png.‎001.png'))
```


In case of the open formulation, we will re-adjust the problem following way. Lets define two new sets- $V := 1,...,n$ that cover only cities and $V' := 0,1,...,n,n+1$ that covers the cities as well as start and end depot. Since the start and end depots are imaginary, we can set the cost to and from all the cities as 0. The way we make it happen is by "wrapping" the cost matrix by 0 distance elements. For instance, if the number of cities that need to be distributed between trucks are 3, then the augmented matrix would look something like this.  

$$\left[\begin{array}{cccc}
0 & 0 & 0 & 0	& 0\\
0 & c_11 & c_12 & c_13 & 0	\\
0 & c_21 & c_22 & c_23 & 0	\\
0 & c_31 & c_32 & c_33 & 0 \\ 
0 & 0 & 0 & 0 & 0
\end{array}\right]$$

With this setup,let's write down the constraints again. 

subject to


$$\sum_{i \in V'} x_{ij} = 1 ,\forall j \in V,\\
\sum_{j \in V'} x_{ij} = 1 ,\forall i \in V,\\
\sum_{i \in V'} x_{i0} = K,\\
\sum_{j \in V'} x_{0j} = K,\\
u_i - u_j +Cx_{ij} \le C - d_j , \forall i, j \in V\\
d_i \le u_i \le C , \forall i \in V $$


Note that majority of the formulation remains the same except for two additional constraints we need to pose. They are around not allowing any tours to start from the destination depot and not allowing any tours to terminate to the start depot. 

$$ 
\sum_{i \in V'}x_{i,0}=0\\
\sum_{i \in V'}x_{n+1,i}=0
$$
With these two additional constraints, we are set. 
